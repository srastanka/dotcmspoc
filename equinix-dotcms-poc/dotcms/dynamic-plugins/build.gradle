subprojects {
    apply plugin: 'java'
    apply plugin: 'osgi'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'org.hidetake.ssh'

    sourceCompatibility = '1.8'

    repositories {
        jcenter()
        maven {
            url 'http://repo.dotcms.com/artifactory/libs-release'
        }
    }

    dependencies {
        compile (group: 'com.dotcms', name: 'dotcms', version: '3.3.1') {
            transitive = true
        }
    }

    task deploy() {
        description 'Deploy a plugin to a specified environment (read from parameter: env, default value: dev).'
        dependsOn jar
        doLast {
            def envConfig = readEnvConfig()
            def targetHostConfig = getTargetHostConfig(envConfig)

            def artifactPath = "${project.buildDir}/libs/${project.name}-${project.version}.jar"
            def deployDest = "${envConfig.dotCMSHome}/dotserver/tomcat-8.0.18/webapps/ROOT/WEB-INF/felix/load"

            println "Deploying dynamic plugin"
            println "  Source file: $artifactPath"
            println "  Destination: ${targetHostConfig.user}@${targetHostConfig.host}:${deployDest}"

            ssh.run {
                session(targetHostConfig) {
                    put from: artifactPath,
                        into: deployDest
                }
            }
        }
    }

    jar.dependsOn test

    defaultTasks 'test', 'jar'
}